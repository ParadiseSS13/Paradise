/**
	The examine override component.

	This component is used to override text generated by examine of a mob or an object.

	Atoms can only have one instance of this component, and therefore only one override.
	Cause having to override examine more than once is kinda dumb.
 */
/datum/component/examine_override
	dupe_mode = COMPONENT_DUPE_UNIQUE
	/// The text that will override the original examine.
	var/text_override

/datum/component/examine_override/Initialize(_text_override)
	if(!isatom(parent))
		return COMPONENT_INCOMPATIBLE

	text_override = _text_override

/datum/component/examine_override/RegisterWithParent()
	RegisterSignal(parent, COMSIG_PARENT_EXAMINE, .proc/Examine)

/datum/component/examine_override/UnregisterFromParent()
	UnregisterSignal(parent, list(COMSIG_PARENT_EXAMINE))

/**
	This proc will trigger when someone examines the parent.
	It handles the override itself.

	Arguments:
	* source: The parent.
	* user: The mob examining the parent.
	* examine_list: The current list of text getting passed from the parent's normal examine() proc.
*/
/datum/component/examine_override/proc/Examine(datum/source, mob/user, list/examine_list)
	examine_list.Cut()
	examine_list += text_override
