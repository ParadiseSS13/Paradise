/datum/game_test/attack_chain_toilet/Run()
	var/datum/test_puppeteer/player = new(src)

	var/obj/structure/toilet/toilet = player.spawn_obj_nearby(/obj/structure/toilet, EAST)
	toilet.open = TRUE
	var/obj/item/reagent_containers/glass/beaker = player.spawn_obj_in_hand(/obj/item/reagent_containers/glass/beaker)
	player.use_item_in_hand()
	player.click_on(toilet)
	TEST_ASSERT_NOT_CHATLOG(player, "You fill")
	player.use_item_in_hand()
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "You fill")
	player.put_away(beaker)

	var/obj/item/flamethrower = player.spawn_obj_in_hand(/obj/item/flamethrower/full/tank)
	player.use_item_in_hand()
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "You torch")
	player.use_item_in_hand()
	player.put_away(flamethrower)

	var/datum/test_puppeteer/victim = player.spawn_puppet_nearby()
	player.set_intent(INTENT_GRAB)
	player.click_on(victim)
	sleep(4 SECONDS) // dunno, should be enough
	player.use_item_in_hand()
	sleep(4 SECONDS)
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "[victim.puppet] needs to")
	player.puppet.forceMove(get_step(player.puppet, EAST))
	player.use_item_in_hand()
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "You give [victim.puppet] a swirlie")
	player.puppet.swap_hand()
	var/obj/item/grab/G = player.puppet.get_active_hand()
	G.clean_grabbed_by(player.puppet, victim.puppet)
	qdel(victim)

	var/obj/item/crowbar = player.spawn_obj_in_hand(/obj/item/crowbar)
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "You lift")
	player.put_away(crowbar)
	player.click_on(toilet)
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "The cistern is empty")
	player.spawn_obj_in_hand(/obj/item/storage/box)
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "You carefully")
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "You find")
	player.puppet.swap_hand()
	player.click_on(toilet)
	TEST_ASSERT_LAST_CHATLOG(player, "You replace")
	qdel(player)

/datum/game_test/attack_chain_shower/Run()
	var/datum/test_puppeteer/player = new(src)

	var/obj/machinery/shower = player.spawn_obj_nearby(/obj/machinery/shower, EAST)
	player.click_on(shower)
	TEST_ASSERT_LAST_CHATLOG(player, "You turn")

	var/obj/item/wrench = player.spawn_obj_in_hand(/obj/item/wrench)
	player.click_on(shower)
	TEST_ASSERT_LAST_CHATLOG(player, "You adjust")

	player.put_away(wrench)
	player.spawn_obj_in_hand(/obj/item/analyzer)
	player.click_on(shower)
	TEST_ASSERT_LAST_CHATLOG(player, "The water temperature")

	player.puppet.swap_hand()
	var/obj/item/welder = player.spawn_obj_in_hand(/obj/item/weldingtool)
	player.use_item_in_hand()
	player.click_on(shower)
	TEST_ASSERT_LAST_CHATLOG(player, "Turn")
	player.put_away(welder)
	player.click_on(shower)
	TEST_ASSERT_LAST_CHATLOG(player, "You turn")
	player.retrieve(welder)
	player.click_on(shower)
	TEST_ASSERT_LAST_CHATLOG(player, "You cut")
	player.put_away(welder)
	qdel(player)

/datum/game_test/attack_chain_sink/Run()
	var/datum/test_puppeteer/player = new(src)

	var/obj/structure/sink = player.spawn_obj_nearby(/obj/structure/sink, SOUTH)
	sink.anchored = FALSE
	player.click_on(sink)
	TEST_ASSERT_LAST_CHATLOG(player, "The sink isn't")
	sink.anchored = TRUE
	player.click_on(sink)
	TEST_ASSERT_LAST_CHATLOG(player, "You wash your")
	player.spawn_obj_in_hand(/obj/item/crowbar)
	player.click_on(sink)
	TEST_ASSERT_LAST_CHATLOG(player, "You wash the")

	player.puppet.swap_hand()
	player.spawn_obj_in_hand(/obj/item/bathroom_parts/sink)
	player.use_item_in_hand()
	TEST_ASSERT_LAST_CHATLOG(player, "You finish")
