//Anything for harm or hostile intents go here (explosions, EMPs, thermite, mutagen)
/datum/chemical_reaction

	explosion_potassium
		name = "Explosion"
		id = "explosion_potassium"
		result = null
		required_reagents = list("water" = 1, "potassium" = 1)
		result_amount = 2
		mix_message = "The mixture explodes!"

		on_reaction(var/datum/reagents/holder, var/created_volume)
			var/datum/effect/system/reagents_explosion/e = new()
			e.set_up(round (created_volume/10, 1), holder.my_atom, 0, 0)
			e.start()
			holder.clear_reagents()
			return

	emp_pulse
		name = "EMP Pulse"
		id = "emp_pulse"
		result = null
		required_reagents = list("uranium" = 1, "iron" = 1) // Yes, laugh, it's the best recipe I could think of that makes a little bit of sense
		result_amount = 2

		on_reaction(var/datum/reagents/holder, var/created_volume)
			var/location = get_turf(holder.my_atom)
			// 100 created volume = 4 heavy range & 7 light range. A few tiles smaller than traitor EMP grandes.
			// 200 created volume = 8 heavy range & 14 light range. 4 tiles larger than traitor EMP grenades.
			empulse(location, round(created_volume / 24), round(created_volume / 14), 1)
			holder.clear_reagents()
			return

	mutagen
		name = "Unstable mutagen"
		id = "mutagen"
		result = "mutagen"
		required_reagents = list("radium" = 1, "plasma" = 1, "chlorine" = 1)
		result_amount = 3
		mix_message = "The substance turns neon green and bubbles unnervingly."

	thermite
		name = "Thermite"
		id = "thermite"
		result = "thermite"
		required_reagents = list("aluminum" = 1, "iron" = 1, "oxygen" = 1)
		result_amount = 3

	glycerol
		name = "Glycerol"
		id = "glycerol"
		result = "glycerol"
		required_reagents = list("cornoil" = 3, "sacid" = 1)
		result_amount = 1

	nitroglycerin
		name = "Nitroglycerin"
		id = "nitroglycerin"
		result = "nitroglycerin"
		required_reagents = list("glycerol" = 1, "facid" = 1, "sacid" = 1)
		result_amount = 2
		on_reaction(var/datum/reagents/holder, var/created_volume)
			var/datum/effect/system/reagents_explosion/e = new()
			e.set_up(round (created_volume/2, 1), holder.my_atom, 0, 0)
			e.start()

			holder.clear_reagents()
			return

	condensedcapsaicin
		name = "Condensed Capsaicin"
		id = "condensedcapsaicin"
		result = "condensedcapsaicin"
		required_reagents = list("capsaicin" = 2)
		required_catalysts = list("plasma" = 5)
		result_amount = 1