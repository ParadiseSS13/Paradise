
/datum/heretic_knowledge_tree_column/main/ash
	neighbour_type_left = /datum/heretic_knowledge_tree_column/cosmic_to_ash
	neighbour_type_right = /datum/heretic_knowledge_tree_column/ash_to_moon

	route = PATH_ASH
	ui_bgr = "node_ash"
	start = /datum/heretic_knowledge/limited_amount/starting/base_ash
	grasp = /datum/heretic_knowledge/ashen_grasp
	tier1 = /datum/heretic_knowledge/spell/ash_passage
	mark = /datum/heretic_knowledge/mark/ash_mark
	ritual_of_knowledge = /datum/heretic_knowledge/knowledge_ritual/ash
	unique_ability = /datum/heretic_knowledge/spell/fire_blast
	tier2 = /datum/heretic_knowledge/mad_mask
	blade = /datum/heretic_knowledge/blade_upgrade/ash
	tier3 =	/datum/heretic_knowledge/spell/flame_birth
	ascension = /datum/heretic_knowledge/ultimate/ash_final

/datum/heretic_knowledge/limited_amount/starting/base_ash
	name = "Nightwatcher's Secret"
	desc = "Opens up the Path of Ash to you. \
		Allows you to transmute a match and a knife into an Ashen Blade. \
		You can only create three at a time."
	gain_text = "The City Guard know their watch. If you ask them at night, they may tell you about the ashy lantern."
	required_atoms = list(
		/obj/item/kitchen/knife = 1,
		/obj/item/match = 1,
	)
	result_atoms = list(/obj/item/melee/sickly_blade/ash)
	research_tree_icon_path = 'icons/obj/weapons/khopesh.dmi'
	research_tree_icon_state = "ash_blade"

/datum/heretic_knowledge/ashen_grasp
	name = "Grasp of Ash"
	desc = "Your Mansus Grasp will burn the eyes of the victim, damaging them and blurring their vision."
	gain_text = "The Nightwatcher was the first of them, his treason started it all. \
		Their lantern, expired to ash - their watch, absent."
	cost = 1
	research_tree_icon_path = 'icons/ui_icons/antags/heretic/knowledge.dmi'
	research_tree_icon_state = "grasp_ash"

/datum/heretic_knowledge/ashen_grasp/on_gain(mob/user, datum/antagonist/heretic/our_heretic)
	RegisterSignal(user, COMSIG_HERETIC_MANSUS_GRASP_ATTACK, PROC_REF(on_mansus_grasp))

/datum/heretic_knowledge/ashen_grasp/on_lose(mob/user, datum/antagonist/heretic/our_heretic)
	UnregisterSignal(user, COMSIG_HERETIC_MANSUS_GRASP_ATTACK)

/datum/heretic_knowledge/ashen_grasp/proc/on_mansus_grasp(mob/living/source, mob/living/target)
	SIGNAL_HANDLER

	if(HAS_TRAIT(target, TRAIT_BLIND))
		return

	var/obj/item/organ/internal/eyes/E = target.get_int_organ(/obj/item/organ/internal/eyes)
	if(!E)
		return

	to_chat(target, "<span class='danger'>A bright green light burns your eyes horrifically!</span>")
	E.receive_damage(9)
	target.EyeBlurry(20 SECONDS)

/datum/heretic_knowledge/spell/ash_passage
	name = "Ashen Passage"
	desc = "Grants you Ashen Passage, a spell that lets you phase out of reality and traverse a short distance, passing though any walls."
	gain_text = "He knew how to walk between the planes."

	action_to_add = /datum/spell/ethereal_jaunt/ash
	cost = 1


/datum/heretic_knowledge/mark/ash_mark
	name = "Mark of Ash"
	desc = "Your Mansus Grasp now applies the Mark of Ash. The mark is triggered from an attack with your Ashen Blade. \
		When triggered, the victim takes additional stamina and burn damage, and the mark is transferred to a nearby heathen. \
		Damage dealt is decreased with each transfer. \
		Triggering the mark will also greatly reduce the cooldown of your Mansus Grasp."
	gain_text = "He was a very particular man, always watching in the dead of night. \
		But in spite of his duty, he regularly tranced through the Manse with his blazing lantern held high. \
		He shone brightly in the darkness, until the blaze begin to die."
	mark_type = /datum/status_effect/eldritch/ash

/datum/heretic_knowledge/mark/ash_mark/trigger_mark(mob/living/source, mob/living/target)
	. = ..()
	if(!.)
		return

	// Also refunds 75% of charge!
	var/datum/spell/touch/mansus_grasp/grasp = locate() in source.actions
	if(grasp && grasp.cooldown_handler.is_on_cooldown())
		grasp.cooldown_handler.start_recharge(grasp.cooldown_handler.recharge_duration * 0.25)

/datum/heretic_knowledge/knowledge_ritual/ash



/datum/heretic_knowledge/spell/fire_blast
	name = "Volcano Blast"
	desc = "This makes you fire immune and heat immune, and grants you Volcano Blast, a spell that - after a short charge - fires off a beam of energy \
		at a nearby enemy, setting them on fire and burning them. If they do not extinguish themselves, \
		the beam will continue to another target."
	gain_text = "No fire was hot enough to rekindle them. No fire was bright enough to save them. No fire is eternal."
	action_to_add = /datum/spell/charge_up/bounce/fire_blast
	cost = 1
	research_tree_icon_frame = 7

/datum/heretic_knowledge/spell/fire_blast/on_gain(mob/user, datum/antagonist/heretic/our_heretic)
	ADD_TRAIT(user, TRAIT_RESISTHEAT, type)
	return ..()

/datum/heretic_knowledge/spell/fire_blast/on_lose(mob/user, datum/antagonist/heretic/our_heretic)
	REMOVE_TRAIT(user, TRAIT_RESISTHEAT, type)
	return ..()


/datum/heretic_knowledge/mad_mask
	name = "Mask of Madness"
	desc = "Allows you to transmute any mask, four candles, a stun baton, and a liver to create a Mask of Madness. \
		The mask instills fear into heathens who witness it, causing stamina damage, hallucinations, and insanity. \
		It can also be forced onto a heathen, to make them unable to take it off..."
	gain_text = "The Nightwatcher was lost. That's what the Watch believed. Yet he walked the world, unnoticed by the masses."
	required_atoms = list(
		/obj/item/organ/internal/liver = 1,
		/obj/item/melee/baton = 1,  // Technically means a cattleprod is valid
		/obj/item/clothing/mask = 1,
		/obj/item/candle = 4,
	)
	result_atoms = list(/obj/item/clothing/mask/madness_mask)
	cost = 1
	research_tree_icon_path = 'icons/obj/clothing/masks.dmi'
	research_tree_icon_state = "mad_mask"

/datum/heretic_knowledge/blade_upgrade/ash
	name = "Fiery Blade"
	desc = "Your blade now lights enemies ablaze on attack."
	gain_text = "He returned, blade in hand, he swung and swung as the ash fell from the skies. \
		His city, the people he swore to watch... and watch he did, as they all burnt to cinders."


	research_tree_icon_path = 'icons/ui_icons/antags/heretic/knowledge.dmi'
	research_tree_icon_state = "blade_upgrade_ash"

/datum/heretic_knowledge/blade_upgrade/ash/do_melee_effects(mob/living/source, mob/living/target, obj/item/melee/sickly_blade/blade)
	if(source == target || !isliving(target))
		return

	target.adjust_fire_stacks(1)
	target.IgniteMob()

/datum/heretic_knowledge/spell/flame_birth
	name = "Nightwatcher's Rebirth"
	desc = "Grants you Nightwatcher's Rebirth, a spell that extinguishes you and \
		burns all nearby heathens who are currently on fire, healing you for every victim afflicted. \
		If any victims afflicted are in critical condition, they will also instantly die."
	gain_text = "The fire was inescapable, and yet, life remained in his charred body. \
		The Nightwatcher was a particular man, always watching."
	action_to_add = /datum/spell/aoe/fiery_rebirth
	cost = 1
	research_tree_icon_frame = 5

/datum/heretic_knowledge/ultimate/ash_final
	name = "Ashlord's Rite"
	desc = "The ascension ritual of the Path of Ash. \
		Bring 3 burning or husked corpses to a transmutation rune to complete the ritual. \
		When completed, you become a harbinger of flames, gaining two abilites. \
		Cascade, which causes a massive, growing ring of fire around you, \
		and Oath of Flame, causing you to passively create a ring of flames as you walk. \
		Some ashen spells you already knew will be empowered as well. \
		You will also become immune to flames, space, and similar environmental hazards."
	gain_text = "The Watch is dead, the Nightwatcher burned with it. Yet his fire burns evermore, \
		for the Nightwatcher brought forth the rite to mankind! His gaze continues, as now I am one with the flames, \
		WITNESS MY ASCENSION, THE ASHY LANTERN BLAZES ONCE MORE!"

	announcement_text = "%SPOOKY% Fear the blaze, for the Ashlord, %NAME% has ascended! The flames shall consume all! %SPOOKY%"
	announcement_sound = 'sound/ambience/antag/heretic/ascend_ash.ogg'
	research_tree_icon_path = 'icons/ui_icons/antags/heretic/ascension.dmi'
	research_tree_icon_state = "ashascend"
	/// A static list of all traits we apply on ascension.
	var/static/list/traits_to_apply = list(
		TRAIT_BOMBIMMUNE,
		TRAIT_NOBREATH,
		TRAIT_NOFIRE,
		TRAIT_RESISTCOLD,
		TRAIT_RESISTHEAT,
		TRAIT_RESISTHIGHPRESSURE,
		TRAIT_RESISTLOWPRESSURE,
		TRAIT_NOSLIP, // so pressure doesn't meme them
	)

/datum/heretic_knowledge/ultimate/ash_final/is_valid_sacrifice(mob/living/carbon/human/sacrifice)
	. = ..()
	if(!.)
		return

	if(sacrifice.on_fire)
		return TRUE
	if(HAS_TRAIT_FROM(sacrifice, TRAIT_HUSK, BURN))
		return TRUE
	return FALSE

/datum/heretic_knowledge/ultimate/ash_final/on_finished_recipe(mob/living/user, list/selected_atoms, turf/our_turf)
	. = ..()
	var/datum/spell/fire_sworn/circle_spell = new(user.mind)
	user.AddSpell(circle_spell)

	var/datum/spell/fire_cascade/big/screen_wide_fire_spell = new(user.mind)
	user.AddSpell(screen_wide_fire_spell)

	var/datum/spell/charge_up/bounce/fire_blast/existing_beam_spell = locate() in user.mob_spell_list
	if(existing_beam_spell)
		existing_beam_spell.max_beam_bounces *= 2 // Double beams
		existing_beam_spell.bounce_time *= 0.66 // Faster beams
		existing_beam_spell.cooldown_handler.recharge_duration *= 0.66 // Lower cooldown

	var/datum/spell/aoe/fiery_rebirth/fiery_rebirth = locate() in user.mob_spell_list
	fiery_rebirth?.base_cooldown *= 0.16

	user.add_traits(traits_to_apply, type)
