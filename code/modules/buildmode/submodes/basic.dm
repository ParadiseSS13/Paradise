/datum/buildmode_mode/basic
	key = "basic"

/datum/buildmode_mode/basic/show_help(mob/user)
	to_chat(user, "<span class='notice'>***********************************************************</span>")
	to_chat(user, "<span class='notice'>Left Mouse Button        = Construct / Upgrade</span>")
	to_chat(user, "<span class='notice'>Right Mouse Button       = Deconstruct / Delete / Downgrade</span>")
	to_chat(user, "<span class='notice'>Left Mouse Button + ctrl = R-Window</span>")
	to_chat(user, "<span class='notice'>Left Mouse Button + alt  = Airlock</span>")
	to_chat(user, "")
	to_chat(user, "<span class='notice'>Use the button in the upper left corner to</span>")
	to_chat(user, "<span class='notice'>change the direction of built objects.</span>")
	to_chat(user, "<span class='notice'>***********************************************************</span>")

/datum/buildmode_mode/basic/handle_click(user, params, obj/object)
	var/list/pa = params2list(params)
	var/left_click = pa.Find("left")
	var/right_click = pa.Find("right")
	var/ctrl_click = pa.Find("ctrl")
	var/alt_click = pa.Find("alt")

	if(istype(object,/turf) && left_click && !alt_click && !ctrl_click)
		var/turf/T = object
		if(istype(object,/turf/space))
			T.ChangeTurf(/turf/simulated/floor/plasteel)
		else if(istype(object,/turf/simulated/floor))
			T.ChangeTurf(/turf/simulated/wall)
		else if(istype(object,/turf/simulated/wall))
			T.ChangeTurf(/turf/simulated/wall/r_wall)
		log_admin("Build Mode: [key_name(user)] built [T] at ([T.x],[T.y],[T.z])")
	else if(right_click)
		log_admin("Build Mode: [key_name(user)] deleted [object] at ([object.x],[object.y],[object.z])")
		if(istype(object,/turf/simulated/wall))
			var/turf/T = object
			T.ChangeTurf(/turf/simulated/floor/plasteel)
		else if(istype(object,/turf/simulated/floor))
			var/turf/T = object
			T.ChangeTurf(/turf/space)
		else if(istype(object,/turf/simulated/wall/r_wall))
			var/turf/T = object
			T.ChangeTurf(/turf/simulated/wall)
		else if(istype(object,/obj))
			qdel(object)
	else if(istype(object,/turf) && alt_click && left_click)
		log_admin("Build Mode: [key_name(user)] built an airlock at ([object.x],[object.y],[object.z])")
		new/obj/machinery/door/airlock(get_turf(object))
	else if(istype(object,/turf) && ctrl_click && left_click)
		var/obj/structure/window/reinforced/WIN = new/obj/structure/window/reinforced(get_turf(object))
		WIN.setDir(BM.build_dir)
		log_admin("Build Mode: [key_name(user)] built a window at ([object.x],[object.y],[object.z])")
